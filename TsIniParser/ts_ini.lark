// Lark grammar definition for TunerStudio INI files
//
// This parser is less tolerant than TunerStudio:
//   key-value pairs: the value must be comma delimited (TunerStudio tolerates spaces)
//   there must be a blank line at the end
//   Expression markers ("{", "}") and parentheses must be balanced
//   All identifiers must be cnames (no spaces, quotes etc.)

%import common (WS_INLINE, NEWLINE, LETTER, DIGIT, WS, ESCAPED_STRING, CNAME, SIGNED_FLOAT,  FLOAT)

%ignore WS_INLINE

// ============================ Overall File Structure ============================

start        : _pre_section_line* _section+
_pre_section_line: kvp_line | _pp_line
_section     : context_help_section | constants_section | generic_section | tableeditor_section | gaugeconfig_section

// ============================ Sections  ============================

// The key-value pairs in this section need special handling.
// The values are just strings - but they can have embedded *unescaped* quotes
// There is no way to parse these as part of normal KVP processing
// Hence special handling
context_help_section : "[SettingContextHelp]" _EOL _help_section_line*
_help_section_line   : _pp_line | help_line 
help_line            : help_line_key string_value _EOL
help_line_key        : KEY
string_value         : STRING_VALUE
STRING_VALUE         : /\".*\"/

// Entries in the Constants section can be grouped into pages 
constants_section : "[Constants]" _EOL _constants_header_line+ page+
_constants_header_line: kvp_line | _pp_line | message_format_line
page              : _PAGE_TAG DECIMAL_INT_CONSTANT _EOL _page_line+
_PAGE_TAG         : "page"i WS_INLINE? "=" 
_page_line        : kvp_line | _pp_line | text_line

// Entries in the TableEditor section can be grouped into tables
tableeditor_section : "[TableEditor]" _EOL table+
table               : _TABLE_TAG table_header _EOL _table_line+
table_header        : table_id "," map3d_id "," title "," page_num ("," code_override)?
table_id          : _identifier
map3d_id          : _identifier
title             : ESCAPED_STRING
page_num          : DECIMAL_INT_CONSTANT
_TABLE_TAG         : "table"i WS_INLINE? "=" 
_table_line        : kvp_line | _pp_line

// Entries in the GaugeConfigurations section can be grouped into gauge categories
gaugeconfig_section     : "[GaugeConfigurations]" _EOL _gaugeconfig_headerline* gauge_category*
gauge_category          : _GAUGE_CATEGORY_TAG ANY_VALUE _EOL _gauge_category_line+
_GAUGE_CATEGORY_TAG     : "gaugeCategory"i WS_INLINE? "=" 
_gauge_category_line    : kvp_line | _pp_line
_gaugeconfig_headerline : kvp_line | _pp_line

// Any other section
generic_section: "[" section_name "]" _EOL _section_line*
_section_line  : kvp_line | defaultvalue_line | _pp_line | text_line | message_format_line
section_name   : SECTION_NAME
SECTION_NAME   : (LETTER | DIGIT) (LETTER | DIGIT | " ")* 

// ============================ Lines ============================

// Key/value pair line. I.e. " key = value "
// General format of key-value pairs:
// name = class,  type, offset, shape, units, scale, translate, lo, hi, digits
kvp_line        : kvp_key _kvp_value _EOL
kvp_key         : KEY
_kvp_value      : kvp_scalar_value | kvp_bit_value | kvp_array_value | kvp_other_value
kvp_scalar_value: "scalar"i "," _field_list
kvp_bit_value   : "bits"i "," _field_list
kvp_array_value : "array"i "," _field_list
?kvp_other_value : _field_list

// Lines beginning with #. Typiclly preprocessor directives
_pp_line       : hashdef | error
hashdef        :_HASHDEF_TAG identifier "=" defined_value _EOL
_HASHDEF_TAG   : "#" WS_INLINE? "define"i 
defined_value  : ANY_VALUE
error          : _HAS_ERROR_TAG ANY_VALUE  _EOL
_HAS_ERROR_TAG : "#" WS_INLINE? "error"i 

defaultvalue_line:  "defaultValue"i "=" identifier "," default_value _EOL
default_value    : ANY_VALUE

text_line      : _TEXTLINE_TAG string_value _EOL
_TEXTLINE_TAG  : "text"i WS_INLINE? "="

message_format_line: "messageEnvelopeFormat" "=" ANY_VALUE _EOL

// ============================ Comma Delimited Field List  ============================

_field_list : (_field_list ",")? _field?
_field      :  inline_expression 
             | variable_ref 
             | type_other 
             | data_type 
             | array_size_1d
             | array_size_2d 
             | bit_size 
             | code_override
             | type_kvp
             | conditional_expression
             | filter_field
variable_ref  : "$" CNAME
array_size_1d   : "[" additive_expression "]"
array_size_2d   : "[" additive_expression "x" additive_expression "]"
bit_size : "[" additive_expression ":" additive_expression "]"
data_type  : DATA_TYPE
DATA_TYPE       : ("S" | "U" | "F") DECIMAL_INT_CONSTANT
inline_expression  : "{" conditional_expression? "}"
code_override: "%code_override"i "=" (_name_override | variable_ref)
_name_override   : _NAME_TAG conditional_expression
_NAME_TAG        : "name"i WS_INLINE? "="
type_kvp        : TYPE_KVP
TYPE_KVP    : (DECIMAL_INT_CONSTANT | CNAME) WS_INLINE? "=" WS_INLINE? (ESCAPED_STRING | NUMBER_FIELD)
NUMBER_FIELD.-1 : FLOATING_CONSTANT | DECIMAL_INT_CONSTANT | BINARY_INT_CONSTANT | HEX_CONSTANT
filter_field       :_FILTER
_FILTER           : /\s*&|=|<|>\s*(?=,)/
type_other        : ANY_FIELD_VALUE
ANY_FIELD_VALUE.-1: /[^,\n]+/

// ============================ Expressions ===========================

// A TS INI file expression statement as a lark grammar.
//
// Not fully tested
//
// Since TS is a Java app, loosley based on https://cs.au.dk/~amoeller/RegAut/JavaBNF.html

// Start point
?conditional_expression : logical_expression | ternary_expression

?primary_expression : identifier
	| _constant
	| string_literal
	| nested_expression
nested_expression: "(" conditional_expression ")"

_postfix_expression : primary_expression
	| array_index_expression
	| fncall_index_expression
	| struct_access_expression
	| postfix_inc_expression
	| postfix_dec_expression

array_index_expression: _postfix_expression "[" conditional_expression "]"
fncall_index_expression: _postfix_expression "(" argument_expression_list? ")"
struct_access_expression: _postfix_expression "." identifier
postfix_inc_expression: _postfix_expression "++"
postfix_dec_expression: _postfix_expression "--"

?argument_expression_list : (argument_expression_list ",")? conditional_expression
	
_unary_expression : _postfix_expression
	| prefix_inc_expression
	| prefix_dec_expression
	| unary_op_expression

prefix_inc_expression: "++" _unary_expression
prefix_dec_expression: "--" _unary_expression
unary_op_expression: _unary_op _unary_expression

!_unary_op : "&"
	| "*"
	| "+"
	| "-"
	| "~"
	| "!"
	
?multiplicative_expression : (multiplicative_expression _multiply_op)? _unary_expression
!_multiply_op: "*" | "/" | "%"

?additive_expression : (additive_expression _additive_op)? multiplicative_expression
!_additive_op: "+" | "-"

?shift_expression : (shift_expression _shift_op)? additive_expression
!_shift_op: "<<" | ">>"

?relational_expression : (relational_expression _relational_op)? shift_expression
!_relational_op: "<" | ">" | "<=" | ">="

?equality_expression : (equality_expression _equality_op)? relational_expression
!_equality_op: "==" | "!="

?bit_expression: (bit_expression _bit_op)? equality_expression
!_bit_op: "&" | "|" | "^"

?logical_expression : (logical_expression _logical_op)? bit_expression
!_logical_op: "&&" | "||"

ternary_expression: logical_expression "?" conditional_expression ":" conditional_expression
	
_constant : _integer_constant
             | char_constant
             | floating_constant

identifier           : CNAME
string_literal       : ESCAPED_STRING
_integer_constant    : binary_constant | decimal_int_constant | hex_constant
decimal_int_constant : DECIMAL_INT_CONSTANT
DECIMAL_INT_CONSTANT : /(?:\+|-)?\d+/
binary_constant      : BINARY_INT_CONSTANT
BINARY_INT_CONSTANT.2: /0b(?:0|1)+/
hex_constant         : HEX_CONSTANT
HEX_CONSTANT.2       : /0x[0-9a-fA-F]+/
char_constant        : /'[^'\\\n]'/ // This is too simplistic. Need to handle escape sequences.
floating_constant    : FLOATING_CONSTANT
FLOATING_CONSTANT    : (FLOAT | SIGNED_FLOAT) "f"?

// ============================ General  ============================

ANY_VALUE      : /[^\n]+/
KEY.-1         : (KEY_NAME | ESCAPED_STRING) WS_INLINE? "="
KEY_NAME       : ("_"|LETTER) ("_"|"-"|LETTER|DIGIT)*
_EOL           : NEWLINE
_identifier    : identifier